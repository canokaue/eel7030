; IDEIA DO PROGRAMA: JOGAR O NRO DE CHAVES DE P2 PARA O ACUMULADOR, ROTACIONAR O ACUMULADOR 
; PARA OS 1S IREM PARA O CARRY E QUANDO IR 1 PARA O CARRY INCREMENTA O CONTADOR, NO CASO O  R7
;  A CADA CHAVE APERTADA NO P2 TERA 1 NO ACUMUALADOR EX: SE ESTIVER UMA CHAVE APERTADA
; INDEPENDENTE DO VALOR QUE ELA REPRESENTA, TERA SÓ UM "1" NO ACUMULADOR, EX: SE EU APERTA A PRIMEIRA CHAVE 
; QUANDO EU PASSAR P2 PARA O ACUMULADOR, O ACUMULADOR RECEBERA "00000001", E EES 1 NA PRIMEIRA POSIÇÃO
; SERÁ ROTACIONADO ATÉ CHEGAR O CARRY, PARA INCREMENTAR R7
; 
CS 			EQU P0.7
END0 		EQU P3.3;
END1 		EQU P3.4;
			
			ORG 0h
			SETB END0 ; passa a porta P3.3 PARA 1
			CLR END1   ;  PASSA A PORTA P3.4 PARA ZERO
			; COM AS DUAS FUNÇÕES A CIMA TEMOS O SEGUNDO DISPLAY DE 7 SEG LIGADO 
			
			SETB CS ; LIGA O FLIPFLOP QUE LIGA OS DISPLAYS 
			
VOLTA:	MOV R7,#0 ; ARMAZENAR A CONTAGEM DAS CHAVES 
		MOV R5,#8 ; AJUDA A VERIFICAR QUANTAS CHAVES ESTÃO APERTADAS,JOGANDO TODOS OS POSSIVEIS 1S
		          ; QUE ESTIVEREM NO ACUMULAR, PARA O CARRY, ROTACIONANDO O CARRY 8 VEZES 
		MOV A,P2 ; ACUMULADOR RECEBE O QUE ESTA SETADO NAS CHAVES -- UM NUMERO QUE VAI AJUDAR A SABER QUANTAS CHAVES ESTAO LEVANTADAS 
		CPL A     ; INVERTE OS BITS DE A, JA QUE P2 MANDA BITS INVERTIDOS 		
	
VAI:	RLC A ;ROTACIONA O CONTEUDO DO ACUMULADOR PARA A ESQUERDA,ATRAVES DO CARRY
			; OU SEJA DA O NUMERO DE CHAVES APERTADAS PARA O CARRY 
			;
		JNC SALTA ; Desvia se o flag carry estiver em 0
		INC R7 ; INCREMENTA R7 QUE INICIALMENTE É 0	
			
SALTA:	DJNZ R5,VAI ; DECREMENTA R5, E SALTA SE NÃO É ZERO
			MOV A,R7
			CALL CONVERTE
			MOV P1,A
			JMP VOLTA
CONVERTE: 	INC A
			MOVC A,@A+PC
			RET
TABELA: 	DB 40H, 79H, 24H, 30H, 19H, 12H, 02H, 78H, 00H, 10H, 08H, 03H, 46H,21H, 06H, 0EH
			END
